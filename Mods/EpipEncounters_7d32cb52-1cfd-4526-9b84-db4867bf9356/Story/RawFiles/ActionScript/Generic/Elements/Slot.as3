package Elements
{
    import LS_Classes.LSPanelHelpers;
    import flash.display.MovieClip;
    import flash.external.ExternalInterface;
    import LS_Classes.listDisplay;
    import LS_Classes.larTween;
    import flash.text.TextField;
    import flash.events.MouseEvent;
    import fl.motion.easing.Quartic;
    import LS_Classes.textEffect;

    public dynamic class Slot extends Element
    {
        // public var bg_mc:MovieClip;
        // public var disable_mc:MovieClip;
        // public var amount_mc:MovieClip;
        // public var refreshSlot_mc:MovieClip;
        // public var unavailable_mc:MovieClip;
        // public var frame_mc:MovieClip;
        // public var source_frame_mc:Shape;
        public var activeHighlight_mc:MovieClip;
        public var highlight_mc:MovieClip;

        public var oldCD:Number = 0;
        private var _dragStartX:Number = 0;
        private var _dragStartY:Number = 0;
        private var _mouseDown:Boolean = false;
        private var startDragDiff:Number = 10;

        // public var enabled:Boolean = true;

        public function Slot()
        {
            super();

            this.setChildIndex(this.bg_mc, 0);

            this.SetEnabled(true);
            this.SetLabel("");
            this.SetSourceBorder(false);
            this.SetWarning(false);
            this.SetActive(false);
            this.SetHighlighted(false);

            this.cd_mc.rot = -90;
            this.cd_mc.cellSize = 50;
            this.cd_mc.visible = false;

            this.refreshSlot_mc.stop();
            this.refreshSlot_mc.visible = false;
            this.refreshSlot_mc.addFrameScript(73, this.onRefreshSlotEnd);

            this.unavailable_mc.visible = false;

            this.addEventListener(MouseEvent.MOUSE_DOWN, this.onMouseDown);
            this.addEventListener(MouseEvent.MOUSE_UP, this.onMouseUp);

            this.SetCooldown(0);
            
            this.amount_mc.amount_txt.filters = textEffect.createStrokeFilter(1050888,1.4,1,1.8,3);

            this.addFrameScript(0, this.frame1);

            // TODO highlight on hover
        }

        public override function get elementType() : String
        {
            return "Slot";
        }

        public function SetActive(active:Boolean) : *
        {
            this.activeHighlight_mc.visible = active;
        }

        public function SetHighlighted(highlighted:Boolean) : *
        {
            this.highlight_mc.visible = highlighted;

            if (highlighted)
            {
                if (this.timeline)
                {
                    this.timeline.stop();
                }

                this.timeline = new larTween(this.highlight_mc, "alpha", Quartic.easeOut, NaN, 0.3, 0.2);
            }
            else
            {
                this.highlight_mc.alpha = 0;
            }
        }

        public function SetCooldown(cooldown:Number, param2:Boolean = true) : *
        {
            if(cooldown == -1) // Set oldCD to 0 so the refresh animation doesn't play.
            {
                this.oldCD = 0;
                cooldown = 0;
            }
            if(cooldown == 0)
            {

                this.cd_mc.visible = false;

                if(this.oldCD != 0)
                {
                    this.refreshSlot_mc.visible = true;
                    this.refreshSlot_mc.alpha = 1;
                    this.refreshSlot_mc.gotoAndPlay(2);
                    ExternalInterface.call("PlaySound","UI_Game_Skill_Cooldown_End");

                    // PIP - set enabled as well
                    if (param2)
                    {
                        this.disable_mc.alpha = 0;
                        this.isEnabled = true;
                    }
                }
            }
            else
            {
                this.cd_mc.visible = true;

                if (param2)
                {
                    this.isEnabled = false;
                    this.disable_mc.alpha = 0; // intentional, so the overlays dont stack
                }
            }
            this.oldCD = cooldown;
            this.cd_mc.setCoolDown(cooldown);
        }

        public function SetEnabled(enabled:Boolean) : *
        {
            this.enabled = enabled;
            this.disable_mc.visible = !enabled;
        }

        public function SetWarning(enabled:Boolean) : *
        {
            this.unavailable_mc.visible = enabled;
        }

        public function SetLabel(text:String) : *
        {
            this.amount_mc.amount_txt.htmlText = text;
        }

        public function SetSourceBorder(enabled:Boolean) : *
        {
            this.source_frame_mc.visible = enabled;
        }

        public function SetIconSize(width:Number, height:Number) : *
        {
            // Changing icon size does not affect click area. TODO center it
        }

        public override function onElementMouseUp(e:MouseEvent) : *
        {
            ExternalInterface.call("elementMouseUp", this.stringID);
        }

        private function onMouseDown(ev:MouseEvent) : *
        {
            this._dragStartX = stage.mouseX;
            this._dragStartY = stage.mouseY;
            this._mouseDown = true;

            stage.addEventListener(MouseEvent.MOUSE_MOVE, this.onMouseMove);
        }

        private function onMouseMove(ev:MouseEvent) : *
        {
            if(this._dragStartX + this.startDragDiff < stage.mouseX || this._dragStartY + this.startDragDiff < stage.mouseY || this._dragStartX - this.startDragDiff > stage.mouseX || this._dragStartY - this.startDragDiff > stage.mouseY)
            {
                stage.removeEventListener(MouseEvent.MOUSE_MOVE,this.onMouseMove);
                ExternalInterface.call("Slot_DragStarted", this.stringID);
                this._mouseDown = false;
            }
        }

        private function onMouseUp(ev:MouseEvent) : *
        {
            if (this._mouseDown)
            {
                ExternalInterface.call("Slot_Clicked", this.stringID);
            }

            stage.removeEventListener(MouseEvent.MOUSE_MOVE, this.onMouseMove);
            this._mouseDown = false;
        }

        private function onRefreshSlotEnd() : *
        {
            this.refreshSlot_mc.stop();
            this.refreshSlot_mc.visible = false;
        }
        
        private function frame1() : *
        {
            
        }
    }
}

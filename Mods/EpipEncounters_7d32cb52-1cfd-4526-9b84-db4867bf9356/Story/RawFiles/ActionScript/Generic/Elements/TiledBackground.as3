package Elements
{
    import LS_Classes.LSPanelHelpers;
    import flash.display.MovieClip;
    import flash.external.ExternalInterface;
    import LS_Classes.listDisplay;
    import flash.text.TextField;
    import flash.events.MouseEvent;

    public dynamic class TiledBackground extends Element
    {
        public var background_mc:MovieClip;
        
        public var backgroundTypes:Array;

        public function TiledBackground()
        {
            super();
            this.elementType = "TiledBackground";

            this.backgroundTypes = new Array();

            this.RegisterType(80, 310, 80, 89, 290, 90);

            this.SetBackground(0, 80 + 310 + 80, 89 + 290 + 90);
        }

        public function SetBackground(backgroundType:int, backgroundWidth:Number, backgroundHeight:Number) : *
        {
            var data:Object = this.backgroundTypes[backgroundType];
            var minWidth:Number = data.Width1 + data.Width2 + data.Width3;
            var minHeight:Number = data.Height1 + data.Height2 + data.Height3;

            this.gotoAndStop(backgroundType + 1);

            if (backgroundWidth < minWidth)
            {
                backgroundWidth = minWidth;
                trace("TiledBackground.SetBackground: Width below minimum");
            }
            if (backgroundHeight < minHeight)
            {
                backgroundHeight = minHeight;
                trace("TiledBackground.SetBackground: Height below minimum");
            }

            var topWidth:Number = backgroundWidth - data.Width1 - data.Width3;
            this.background_mc.top_shp.width = topWidth;
            this.background_mc.bottom_shp.width = topWidth + 1;

            var sideHeight:Number = backgroundHeight - data.Height1 - data.Height3;

            this.background_mc.left_shp.height = sideHeight;
            this.background_mc.right_shp.height = sideHeight;

            this.background_mc.center_shp.width = topWidth;
            this.background_mc.center_shp.height = sideHeight;

            this.background_mc.top_shp.x = data.Width1;
            this.background_mc.top_right_shp.x = backgroundWidth - data.Width3;

            // Row 2

            this.background_mc.left_shp.y = data.Height1;

            this.background_mc.center_shp.x = data.Width1;
            this.background_mc.center_shp.y = data.Height1;

            this.background_mc.right_shp.x = backgroundWidth - data.Width3;
            this.background_mc.right_shp.y = data.Height1;

            // Row 3

            this.background_mc.bottom_left_shp.y = backgroundHeight - data.Height3 - 0.5;

            this.background_mc.bottom_shp.x = data.Width1;
            this.background_mc.bottom_shp.y = backgroundHeight - data.Height3 - 0.5;

            this.background_mc.bottom_right_shp.x = backgroundWidth - data.Width3 + 1; // TODO
            this.background_mc.bottom_right_shp.y = backgroundHeight - data.Height3 - 0.5;
            
        }

        private function RegisterType(width1:Number, width2:Number, width3:Number, height1:Number, height2:Number, height3:Number) : *
        {
            var obj:Object = new Object();

            obj.Width1 = width1;
            obj.Width2 = width2;
            obj.Width3 = width3;

            obj.Height1 = height1;
            obj.Height2 = height2;
            obj.Height3 = height3;

            this.backgroundTypes.push(obj);
        }
    }
}

package Elements
{
    import LS_Classes.LSPanelHelpers;
    import flash.display.MovieClip;
    import flash.external.ExternalInterface;
    import LS_Classes.listDisplay;
    import flash.text.TextField;
    import flash.events.MouseEvent;

    public dynamic class Divider extends Element
    {
        public var _divider_Elegant:MovieClip;
        public var _divider_Line:MovieClip;
        public var _divider_Border:MovieClip;

        public var divider_mc:MovieClip = null;

        public var visualTypes:Object = null;

        public function Divider()
        {
            super();
            this.visualTypes = new Object();

            this.RegisterType("Elegant", this._divider_Elegant);
            this.RegisterType("Line", this._divider_Line);
            this.RegisterType("Border", this._divider_Border);

            this.SetType("Elegant");
            this.SetSize(100, 10);
        }

        public override function get elementType() : String
        {
            return "Divider";
        }

        private function RegisterType(stringID:String, mc:MovieClip) : *
        {
            var obj:Object = new Object();

            obj.MovieClip = mc;
            
            this.visualTypes[stringID] = obj;

            this.removeChild(mc);
        }

        public function SetType(dividerType:String) : *
        {
            if (this.divider_mc)
            {
                this.removeChild(this.divider_mc);
            }

            var visualData:Object = this.visualTypes[dividerType];

            if (visualData)
            {
                this.divider_mc = visualData.MovieClip;

                this.addChild(this.divider_mc);
                this.setChildIndex(this.divider_mc, 0);

                this.divider_mc.visible = true;
                this.divider_mc.scaleX = 1;
                this.divider_mc.scaleY = 1;
            }
            else
            {
                this.Log("SetType(): invalid visual type " + dividerType, 2);
            }
        }

        public override function SetSize(width:Number, height:Number) : *
        {
            var centerLength:Number = width;

            // For dividers with left, right and center pieces
            if (this.divider_mc.left_shp)
            {
                centerLength = width - this.divider_mc.left_shp.width - this.divider_mc.right_shp.width;

                this.divider_mc.line_shp.x = this.divider_mc.left_shp.width;
                this.divider_mc.right_shp.x = width - this.divider_mc.right_shp.width;


                // Centerpiece
                if (this.divider_mc.center_shp)
                {
                    this.divider_mc.center_shp.x = width / 2 - this.divider_mc.center_shp.width / 2;
                    this.divider_mc.center_shp.y = this.divider_mc.line_shp.height / 2 - this.divider_mc.center_shp.height / 2;
                }
            }

            this.divider_mc.line_shp.width = centerLength;
        }
    }
}

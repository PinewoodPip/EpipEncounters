Version 1
SubGoalCombiner SGC_AND
INITSECTION

DB_AMER_Dyes("AMER_TOOL_Dye_Abyss_c7cf04d2-1bcf-4223-a245-a32dcb98c457");
DB_AMER_Dyes("AMER_TOOL_Dye_Clay_95f74ac5-ec9c-4018-b8ac-b5099bcf319a");
DB_AMER_Dyes("AMER_TOOL_Dye_Coral_4b4b1e09-417b-4319-8e04-b859db48d077");
DB_AMER_Dyes("AMER_TOOL_Dye_Earth_f60a93bb-0d6c-459f-af62-9a3472d93374");
DB_AMER_Dyes("AMER_TOOL_Dye_Fog_923c1158-5972-456d-813a-1655fa4c175a");

DB_AMER_Dyes("AMER_TOOL_Dye_Lichen_bbe83a2c-9ed9-473a-96ab-0b71809b42a5");
DB_AMER_Dyes("AMER_TOOL_Dye_Midnight_41d07754-b3ae-4100-85ed-8d174e940f8a");
DB_AMER_Dyes("AMER_TOOL_Dye_Nemesis_b8324054-f302-4309-a956-82a9f569d3dc");
DB_AMER_Dyes("AMER_TOOL_Dye_Saffron_4bd3a715-3688-40b2-9940-4fbd3b4655e7");
DB_AMER_Dyes("AMER_TOOL_Dye_Seafoam_799fe8de-d76f-405e-a0d6-c2481d1a864d");
DB_AMER_Dyes("AMER_TOOL_Dye_Smoke_ce1e8c3e-8067-4b96-bf4b-0519521101fd");
DB_AMER_Dyes("AMER_TOOL_Dye_Stealth_2e776366-3fd1-4ad9-8002-fc61026c9809");
DB_AMER_Dyes("AMER_TOOL_Dye_Verdure_a4f8691b-9d56-4e98-92c7-404e9ec6bfb1");
DB_AMER_Dyes("AMER_TOOL_Dye_Void_a74799d1-d54f-4ad3-bb93-a46ad98ce06b");

DB_AMER_Dyes("TOOL_Dye_Black_0f000ee0-e47a-47aa-888a-b54742467fac");
DB_AMER_Dyes("TOOL_Dye_Blue_cdffcc74-44a4-4f95-a1ce-5f7bd11e1dc5");
DB_AMER_Dyes("TOOL_Dye_Green_49a19fd6-8035-4f4f-bd61-95e85b4b91e3");
DB_AMER_Dyes("TOOL_Dye_Purple_4a02aa47-05e2-4442-9936-6683963051f0");
DB_AMER_Dyes("TOOL_Dye_Red_5284e851-3f59-45b7-bce6-cdafdd98da25");
DB_AMER_Dyes("TOOL_Dye_White_75236993-92c7-482b-aba6-7b54b791ed7c");
DB_AMER_Dyes("TOOL_Dye_Yellow_4e6aedcb-75f8-4b05-8ee5-f86b5a86845d");
DB_AMER_Dyes("TOOL_UNI_Dye_A_Mystery_0e9a6aa9-5e66-45ed-8d2c-99aa02659bd6");

KBSECTION

IF
ItemTemplateCombinedWithItemTemplate(_Root1, _Root2, _Root3, _Root4, _Root5, _Char, _Item)
AND
QRY_AMER_TUT_Dyes_DyeUsed(_Root1, _Root2, _Root3, _Root4, _Root5)
AND
QRY_AMER_GEN_GetEquippedItemSlot(_Char, _Item)
AND
DB_AMER_GEN_OUTPUT_String(_EquipSlot)
AND
_EquipSlot != "None"
THEN
PROC_PIP_ReEquipItem(_Char, _Item);

PROC PROC_PIP_ReEquipItem((CHARACTERGUID)_Char, (ITEMGUID)_Item) // separate PROC so it can be reused in the future
THEN
PROC_AMER_TempStat_FlexStat_Add(_Char, "Attribute", "Strength", 10.0, 200, "");
PROC_AMER_TempStat_FlexStat_Add(_Char, "Attribute", "Intelligence", 10.0, 200, "");
PROC_AMER_TempStat_FlexStat_Add(_Char, "Attribute", "Finesse", 10.0, 200, "");
PROC_AMER_TempStat_FlexStat_Add(_Char, "Attribute", "Memory", 10.0, 200, "");
CharacterUnequipItem(_Char, _Item);
DB_PIP_ItemBeingReEquipped(_Char, _Item);
ProcObjectTimer(_Char, "PIP_ReEquipItem", 50); // everything in this engine is powered by timers

PROC ProcObjectTimerFinished((CHARACTERGUID)_Char, "PIP_ReEquipItem")
AND
DB_PIP_ItemBeingReEquipped(_Char, _Item)
THEN
CharacterEquipItem(_Char, _Item);
NOT DB_PIP_ItemBeingReEquipped(_Char, _Item);

// REGION Check if a dye was used - copy-pasted from tutorial script, since it can possibly be disabled

QRY QRY_AMER_TUT_Dyes_DyeUsed((STRING)_Root1, (STRING)_Root2, (STRING)_Root3, (STRING)_Root4, (STRING)_Root5)
AND
DB_AMER_Dyes(_DyeRoot)
AND
QRY_AMER_TUT_Dyes_DyeUsed(_DyeRoot, _Root1, _Root2, _Root3, _Root4, _Root5)
THEN
DB_NOOP(1);

QRY QRY_AMER_TUT_Dyes_DyeUsed((STRING)_DyeRoot, (STRING)_Root1, (STRING)_Root2, (STRING)_Root3, (STRING)_Root4, (STRING)_Root5)
AND
_Root1 == _DyeRoot
THEN
DB_NOOP(1);
// NRD_ModCall("EpipEncounters", "TestPrint", _Root1);

QRY QRY_AMER_TUT_Dyes_DyeUsed((STRING)_DyeRoot, (STRING)_Root1, (STRING)_Root2, (STRING)_Root3, (STRING)_Root4, (STRING)_Root5)
AND
_Root2 == _DyeRoot
THEN
DB_NOOP(1);
// NRD_ModCall("EpipEncounters", "TestPrint", _Root2);

QRY QRY_AMER_TUT_Dyes_DyeUsed((STRING)_DyeRoot, (STRING)_Root1, (STRING)_Root2, (STRING)_Root3, (STRING)_Root4, (STRING)_Root5)
AND
_Root3 == _DyeRoot
THEN
DB_NOOP(1);
// NRD_ModCall("EpipEncounters", "TestPrint", _Root3);

QRY QRY_AMER_TUT_Dyes_DyeUsed((STRING)_DyeRoot, (STRING)_Root1, (STRING)_Root2, (STRING)_Root3, (STRING)_Root4, (STRING)_Root5)
AND
_Root4 == _DyeRoot
THEN
DB_NOOP(1);
// NRD_ModCall("EpipEncounters", "TestPrint", _Root4);

QRY QRY_AMER_TUT_Dyes_DyeUsed((STRING)_DyeRoot, (STRING)_Root1, (STRING)_Root2, (STRING)_Root3, (STRING)_Root4, (STRING)_Root5)
AND
_Root5 == _DyeRoot
THEN
DB_NOOP(1);
// NRD_ModCall("EpipEncounters", "TestPrint", _Root5);


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "EEModWrapper"
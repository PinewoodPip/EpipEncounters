Version 1
SubGoalCombiner SGC_AND
INITSECTION

DB_AMER_ModSettings_MetaSkills("Target_PIP_META_SP");
DB_AMER_ModSettings_MetaSkills("Target_PIP_META_Resistances");
DB_AMER_GEN_SkillIsMetaSkill("Target_PIP_META_SP");
DB_AMER_GEN_SkillIsMetaSkill("Target_PIP_META_Resistances");

PROC_AMER_GEN_TSK_Add("EN", "PIP_META_SP", "has [1]/[2] Source Points.");
PROC_AMER_GEN_TSK_Add("EN", "PIP_META_Resistances", "has [3]/[4]% [1] resistance ([2] Rating).");
PROC_AMER_GEN_TSK_Add("EN", "PIP_META_Resistances_NoDR", "has [2]/[3]% [1] resistance.");

// res flexstats.
DB_PIP_Resistances("FIRERESISTANCE", "Fire");
DB_PIP_Resistances("WATERRESISTANCE", "Water");
DB_PIP_Resistances("AIRRESISTANCE", "Air");
DB_PIP_Resistances("EARTHRESISTANCE", "Earth");
DB_PIP_Resistances("POISONRESISTANCE", "Poison");
DB_PIP_Resistances("PHYSICALRESISTANCE", "Physical");
DB_PIP_Resistances("PIERCINGRESISTANCE", "Piercing");

KBSECTION

IF
CharacterUsedSkillOnTarget(_Char, (CHARACTERGUID)_Target, "Target_PIP_META_SP", _, _)
AND
DB_AMER_Source_CurrentPoints(_Target, _SourcePoints)
AND
CharacterGetMaxSourcePoints(_Target, _MaxSource)
// AND
// QRY_AMER_GEN_TSK_Get("PIP_META_SP")
// AND
// DB_AMER_GEN_OUTPUT_String(_String)
THEN
PROC_AMER_GEN_CharacterCombatLogText_Param_IntInt(_Target, "has [1]/[2] Source Points.", _SourcePoints, _MaxSource);

// pre-init
IF
CharacterUsedSkillOnTarget(_Char, (CHARACTERGUID)_Target, "Target_PIP_META_SP", _, _)
AND
NOT DB_AMER_Source_CurrentPoints(_Target, _)
THEN
PROC_AMER_GEN_CharacterCombatLogText(_Target, "has not yet acted in combat; cannot check Source Points.");

// DR mode
IF
CharacterUsedSkillOnTarget(_Char, (CHARACTERGUID)_Target, "Target_PIP_META_Resistances", _, _)
AND
DB_PIP_Resistances(_FlexStat, _FriendlyName)
AND
DB_AMER_ResistanceRating(_Target, _FlexStat, _ResistanceDR, _ResistanceRating)
AND
Integer(_ResistanceDR, _ResistanceDRInt)
AND
Integer(_ResistanceRating, _ResistanceRatingInt)
AND
QRY_PIP_GetMaxRes(_Target)
AND
DB_PIP_MaxRes(_MaxRes)
AND
Integer(_MaxRes, _MaxResInt)
THEN
PROC_PIP_PrintResistances(_Target, _FriendlyName, _ResistanceRatingInt, _ResistanceDRInt, _MaxResInt);

// no DR
IF
CharacterUsedSkillOnTarget(_Char, (CHARACTERGUID)_Target, "Target_PIP_META_Resistances", _, _)
AND
NOT DB_AMER_ResistanceRating(_Target, _, _, _)
AND
DB_PIP_Resistances(_FlexStat, _FriendlyName)
AND
StringConcatenate("AMER_GEN_", _FriendlyName, _Concat1)
AND
StringConcatenate(_Concat1, "Resistance", _Var)
AND
GetVarFloat(_Target, _Var, _Res)
AND
QRY_PIP_GetMaxRes(_Target)
AND
DB_PIP_MaxRes(_MaxRes)
AND
Integer(_MaxRes, _MaxResInt)
AND
Integer(_Res, _ResInt)
THEN
PROC_PIP_PrintResistances(_Target, _FriendlyName, _ResInt, _MaxResInt);

QRY QRY_PIP_GetMaxRes((CHARACTERGUID)_Char)
AND
DB_PIP_MaxRes(_Amount)
THEN
NOT DB_PIP_MaxRes(_Amount);

QRY QRY_PIP_GetMaxRes((CHARACTERGUID)_Char)
AND
DB_AMER_MaxRes_Max(_Char, _FlexStat, _MaxRes)
THEN
DB_PIP_MaxRes(_MaxRes);

QRY QRY_PIP_GetMaxRes((CHARACTERGUID)_Char)
AND
NOT DB_AMER_MaxRes_Max(_Char, _, _)
THEN
DB_PIP_MaxRes(100.0);

PROC PROC_PIP_PrintResistances((CHARACTERGUID)_Char, (STRING)_ResName, (INTEGER)_ResistanceRating, (INTEGER)_ResistanceDR, (INTEGER)_MaxRes)
AND
IntegertoString(_MaxRes, _MaxResStr)
AND
IntegertoString(_ResistanceDR, _ResistanceDRStr)
AND
IntegertoString(_ResistanceRating, _ResistanceRatingStr)
AND
StringConcatenate("has ", _ResistanceDRStr, _Concat1)
AND
StringConcatenate(_Concat1, "/", _Concat2)
AND
StringConcatenate(_Concat2, _MaxResStr, _Concat3)
AND
StringConcatenate(_Concat3, "% ", _Concat4)
AND
StringConcatenate(_Concat4, _ResName, _Concat5)
AND
StringConcatenate(_Concat5, " resistance ", _Concat6)
AND
StringConcatenate(_Concat6, "(", _Concat7)
AND
StringConcatenate(_Concat7, _ResistanceRatingStr, _Concat8)
AND
StringConcatenate(_Concat8, " rating).", _Exodia)
THEN
PROC_AMER_GEN_CharacterCombatLogText(_Char, _Exodia);

PROC PROC_PIP_PrintResistances((CHARACTERGUID)_Char, (STRING)_ResName, (INTEGER)_ResistanceDR, (INTEGER)_MaxRes)
AND
IntegertoString(_MaxRes, _MaxResStr)
AND
IntegertoString(_ResistanceDR, _ResistanceDRStr)
AND
StringConcatenate("has ", _ResistanceDRStr, _Concat1)
AND
StringConcatenate(_Concat1, "/", _Concat2)
AND
StringConcatenate(_Concat2, _MaxResStr, _Concat3)
AND
StringConcatenate(_Concat3, "% ", _Concat4)
AND
StringConcatenate(_Concat4, _ResName, _Concat5)
AND
StringConcatenate(_Concat5, " resistance.", _BabyExodia)
THEN
PROC_AMER_GEN_CharacterCombatLogText(_Char, _BabyExodia);

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start_AMER"
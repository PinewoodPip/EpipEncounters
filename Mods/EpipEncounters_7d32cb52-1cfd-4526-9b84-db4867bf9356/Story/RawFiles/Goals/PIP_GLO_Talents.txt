Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

// Scripting for the custom talents feature.

IF CharacterCreationFinished(_Char)
AND
String(_Char, _CharStr)
AND
DB_PIP_Talents_Pending(_Char, _Talent, _Change)
AND
IntegertoString(_Change, _ChangeStr)
THEN
NOT DB_PIP_Talents_Pending(_Char, _Talent, _Change);
NRD_ModCall("EpipEncounters", "TalentsToggle", _CharStr, _Talent, _ChangeStr);
// PROC_AMER_GEN_CharacterCombatLogText(_Char, _Talent);

PROC PROC_PIP_Talents_TogglePending((CHARACTERGUID)_Char, (STRING)_Talent, (INTEGER)_Toggle)
AND
DB_PIP_Talents_Pending(_Char, _Talent, _OldChange)
THEN
NOT DB_PIP_Talents_Pending(_Char, _Talent, _OldChange);
DB_PIP_Talents_Pending(_Char, _Talent, _Toggle);

PROC PROC_PIP_Talents_TogglePending((CHARACTERGUID)_Char, (STRING)_Talent, (INTEGER)_Toggle)
AND
NOT DB_PIP_Talents_Pending(_Char, _Talent, _)
THEN
DB_PIP_Talents_Pending(_Char, _Talent, _Toggle);

// while respeccing, we have to temporarily add talent points to avoid an issue with swapping custom talents for real ones.
PROC PROC_PIP_Talents_RespecRememberTruePoints((CHARACTERGUID)_Char, (INTEGER)_Points)
AND
DB_PIP_Talents_PreRespecTalentPoints(_Char, _OldPoints, _FakePoints)
THEN
NOT DB_PIP_Talents_PreRespecTalentPoints(_Char, _OldPoints, _FakePoints);

PROC PROC_PIP_Talents_RespecRememberTruePoints((CHARACTERGUID)_Char, (INTEGER)_FakePoints)
AND
CharacterGetReservedUserID(_Char, _User)
AND
NOT DB_AMER_GEN_PlayerUsingController(_User)
AND
CharacterGetTalentPoints(_Char, _Points)
THEN
DB_PIP_Talents_PreRespecTalentPoints(_Char, _Points, _FakePoints);
CharacterAddTalentPoint(_Char, _FakePoints);

IF CharacterCreationFinished(_Char)
AND
DB_PIP_Talents_PreRespecTalentPoints(_Char, _, _)
THEN
PROC_PIP_RemoveFakeTalentPoints(_Char);

IF SavegameLoaded(_, _, _, _)
AND
DB_PIP_Talents_PreRespecTalentPoints(_Char, _, _)
THEN
PROC_PIP_RemoveFakeTalentPoints(_Char);

PROC PROC_PIP_RemoveFakeTalentPoints((CHARACTERGUID)_Char)
AND
DB_PIP_Talents_PreRespecTalentPoints(_Char, _Points, _FakePointsStored)
AND
CharacterGetTalentPoints(_Char, _FakePoints)
AND
IntegerMin(_FakePoints, _FakePointsStored, _FakePointsToRemove)
AND
IntegerProduct(_FakePointsToRemove, -1, _Change)
THEN
CharacterAddTalentPoint(_Char, _Change);
// PROC_AMER_GEN_CharacterCombatLogText(_Char, "cleaned up points");
NOT DB_PIP_Talents_PreRespecTalentPoints(_Char, _Points, _FakePointsStored);

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start_AMER"
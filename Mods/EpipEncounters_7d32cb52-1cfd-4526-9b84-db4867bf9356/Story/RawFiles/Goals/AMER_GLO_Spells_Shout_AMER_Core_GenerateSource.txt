Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

// Grant 1 SP if Max SP != Current SP and consume 2 turns of Source Generation, else extend Source Gen if we're already at max or cannot pay 2 turns.

// I had to add the per-round check to prevent an issue with both procs succeding (first the extend one, then the consume one) - I couldn't figure out how to solve it otherwise. Interestingly, this problem only started to happen after adding Blitz Mode.

PROC PROC_AMER_Spells_SkillCast_PerTarget((CHARACTERGUID)_Char, "Shout_AMER_Core_GenerateSource", (GUIDSTRING)_Target, (REAL)_X, (REAL)_Y, (REAL)_Z, (INTEGER)_InfusCount, (STRING)_Type, (STRING)_Element)
AND
QRY_AMER_Source_SourceGen_GetMaxSPAllowed(_Char)
AND
DB_AMER_GEN_OUTPUT_Integer(_MaxSP)
AND
_MaxSP > 0
AND
CharacterGetSourcePoints(_Char, _SPCur)
THEN
PROC_AMER_Spells_SourceGen_Exec(_Char, _MaxSP, _SPCur);

// Extend Source Generation.
PROC PROC_AMER_Spells_SourceGen_Exec((CHARACTERGUID)_Char, (INTEGER)_MaxSP, (INTEGER)_SPCur)
AND
QRY_PIP_Source_CanRegainSourceGenerationFromProto(_Char, _MaxSP, _SPCur)
AND
QRY_AMER_GEN_OncePerRound(_Char, "PIP_PROTO")
THEN
PROC_AMER_Source_SourceGen_AddMaxGen_ForCombat(_Char, 1);
PROC_AMER_GEN_CharacterCombatLogText(_Char, "AMER_Ascension_Prototrophize_GenExtend");
CharacterStatusText(_Char, "PIP_PrototrophizeSourceGenExtended");
DB_AMER_GEN_OncePerRound(_Char, "PIP_PROTO");

// Consume 2 turns of Source Generation and directly grant a Source Point.
PROC PROC_AMER_Spells_SourceGen_Exec((CHARACTERGUID)_Char, (INTEGER)_MaxSP, (INTEGER)_SPCur)
AND
NOT QRY_PIP_Source_CanRegainSourceGenerationFromProto(_Char, _MaxSP, _SPCur)
AND
DB_AMER_Source_SourceGen(_Char, _TurnCount, _SPGained)
AND
QRY_AMER_GEN_OncePerRound(_Char, "PIP_PROTO")
AND
IntegerSum(_SPGained, 2, _NewSourceGainFromGen)
THEN
PROC_AMER_CharacterAddSourcePoints(_Char, 1);
NOT DB_AMER_Source_SourceGen(_Char, _TurnCount, _SPGained);
DB_AMER_Source_SourceGen(_Char, _TurnCount, _NewSourceGainFromGen); // update source db to make it think we used up +2 turns.
DB_AMER_GEN_OncePerRound(_Char, "PIP_PROTO");

// is at max sp - cannot gain SP directly
QRY QRY_PIP_Source_CanRegainSourceGenerationFromProto((CHARACTERGUID)_Char, (INTEGER)_MaxSP, (INTEGER)_SPCur)
AND
_MaxSP == _SPCur
THEN
DB_NOOP();

// is at 1 or less turns of source generation - cannot gain SP directly
QRY QRY_PIP_Source_CanRegainSourceGenerationFromProto((CHARACTERGUID)_Char, (INTEGER)_MaxSP, (INTEGER)_SPCur)
AND
DB_AMER_Source_SourceGen(_Char, _TurnCount, _SPGained)
AND
QRY_AMER_Source_SourceGen_GetMaxGenTurns(_Char)
AND
DB_AMER_SourceGen_OUTPUT_MaxGenTurns(_MaxGenTurns)
AND
IntegerSubtract(_MaxGenTurns, _SPGained, _GenRemaining)
AND
IntegertoString(_GenRemaining, _Str)
AND
_GenRemaining < 2
AND
NOT DB_AMER_ExtendedStat_AddedStat(_Char, "SourceGen_InfiniteDuration", _, _, _, _) // infinite gen fails the >2 source gen check.
THEN
DB_NOOP();
// CharacterStatusText(_Char, _Str);

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_Spells_Special"
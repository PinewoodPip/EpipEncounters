Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//Source Infusion 1: if you have 3 stacks of Feedback or more when you perform an Elementalist reaction, emulate the effects of Dazing Bolt, Fireball, Impalement, or Winter Blast with air, fire, water, and earth reactions, respectively.

PROC PROC_AMER_Reaction_Fire((CHARACTERGUID)_Char, (CHARACTERGUID)_Target, (REAL)_X, (REAL)_Y, (REAL)_Z, "AMER_Elementalist", (STRING)_RequestID, (INTEGER)_RitualBool)
AND
DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Elementalist_MUTA_EmulateSkillSI", _)
AND
QRY_AMER_Source_Infusion_GetCount(_Char, "Ascension_Elementalist_MUTA_EmulateSkillSI")
AND
DB_AMER_GEN_OUTPUT_Integer(_InfusCount)
AND
_InfusCount > 0
AND
QRY_AMER_KeywordStat_Elementalist_Feedback_GetStacksAndType(_Char)
AND
DB_AMER_GEN_OUTPUT_Integer(_Stacks)
AND
DB_AMER_GEN_OUTPUT_String(_Type)
AND
_Stacks >= 3
AND
StringConcatenate("AMER_Ascension_Elementalist_MUTA_EmulateSkillSI_", _Type, _LogText)
THEN
DB_AMER_Source_Infusion_RefundPoints(_Char, 0, 1); // PIP
PROC_AMER_Source_Infusion_ExpendInfusion(_Char, "Ascension_Elementalist_MUTA_EmulateSkillSI", 1, 1, 1);
PROC_AMER_GEN_CharacterCombatLogText(_Char, _LogText);
PROC_AMER_Ascension_Elementalist_MUTA_EmulateSkillSI(_Char, _Target, _X, _Y, _Z, _Type);


PROC PROC_AMER_Ascension_Elementalist_MUTA_EmulateSkillSI((CHARACTERGUID)_Char, (CHARACTERGUID)_Target, (REAL)_X, (REAL)_Y, (REAL)_Z, "Fire")
THEN
PROC_AMER_GEN_CharacterCauseExplosion(_Char, _Target, "Projectile_Fireball");

PROC PROC_AMER_Ascension_Elementalist_MUTA_EmulateSkillSI((CHARACTERGUID)_Char, (CHARACTERGUID)_Target, (REAL)_X, (REAL)_Y, (REAL)_Z, "Water")
THEN
PlayEffectAtPosition("RS3_FX_Skills_Water_WinterBlast_Impact_01", _X, _Y, _Z);
PROC_AMER_GEN_CharacterCauseExplosion(_Char, _Target, "Projectile_AMER_SCRIPT_Winterblast");

PROC PROC_AMER_Ascension_Elementalist_MUTA_EmulateSkillSI((CHARACTERGUID)_Char, (CHARACTERGUID)_Target, (REAL)_X, (REAL)_Y, (REAL)_Z, "Air")
THEN
PROC_AMER_GEN_ShootProjectileStrikeAtPos(_Char, _X, _Y, _Z, "ProjectileStrike_DazingBolt");

PROC PROC_AMER_Ascension_Elementalist_MUTA_EmulateSkillSI((CHARACTERGUID)_Char, (CHARACTERGUID)_Target, (REAL)_X, (REAL)_Y, (REAL)_Z, "Earth")
THEN
PROC_AMER_GEN_ShootProjectileStrikeAtPos(_Char, _X, _Y, _Z, "Projectile_AMER_SCRIPT_RockSpikes_Infusion_2");

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_UI_Ascension_NodeRewards"
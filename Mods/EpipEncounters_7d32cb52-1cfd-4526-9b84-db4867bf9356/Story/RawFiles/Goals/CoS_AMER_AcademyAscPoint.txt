Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

// PIP EDIT - only grant this point if the point from entering Amadia Sanctuary has not been granted - this should only happen on a save that installed Epip Encounters after act 1 (or never happened to stumble upon the camp, I guess!)
// This is to ensure old saves that are past act 1 do not miss out on an Ascension point by the end of the game.

//Grant one Ascension point to the entire party when a player enters the Academy for the first time, then complete this goal.
IF
CharacterEnteredTrigger(_Char, TRIGGERGUID_S_CoS_SUB_Academy_51ddb828-18a6-4b6e-9f5a-e2b2883862e1)
AND
DB_IsPlayer(_Char)  //Ensure the entering character is a player, though probably redundant because I'm pretty sure this trigger is only registered for players.
AND
NOT DB_PIP_HasAscensionPointFromAmadiaSanctuary(1) // Do not grant this point if the seeker camp one has been granted.
THEN
PROC_AMER_ORI_AscPointFromAcademy();

// PIP - I don't think I can turn this off in advance, but I'm not sure.
IF
CharacterEnteredTrigger(_Char, TRIGGERGUID_S_CoS_SUB_Academy_51ddb828-18a6-4b6e-9f5a-e2b2883862e1)
AND
DB_IsPlayer(_Char)
AND
DB_PIP_HasAscensionPointFromAmadiaSanctuary(1)
THEN
GoalCompleted;

PROC PROC_AMER_ORI_AscPointFromAcademy()
THEN
PROC_AMER_UI_Ascension_TotalPointsFound_CheckSet_FromSource(1, "Academy");

PROC PROC_AMER_ORI_AscPointFromAcademy()
AND
DB_IsPlayer(_Player)
THEN
PROC_AMER_UI_Ascension_PointsRemaining_TryAdd_FromSource(_Player, 1, "Academy");

PROC PROC_AMER_ORI_AscPointFromAcademy()
THEN
GoalCompleted;

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "CoS_Academy"
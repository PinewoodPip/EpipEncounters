Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

// does not work unless this goal goes alphabetically first among the Start_AMER ones. Possibly needs to go before the Greatforge one containing the reduce operation.

// Grant Artifact Fragments ("Clouded Memories") on Dismantle (Reduce)
PROC PROC_AMER_UI_Greatforge_DoCraft((INTEGER)_Instance, (CHARACTERGUID)_Char, (ITEMGUID)_Cont, (ITEMGUID)_Item, (REAL)_Level, (STRING)_ItemType, (STRING)_Slot, (STRING)_SubType, (INTEGER)_Handedness, (REAL)_Value, "Reduce")
AND
QRY_AMER_Artifact_ItemIsArtifact(_Item)
THEN
ItemTemplateAddTo("PIP_LOOT_UNI_ArtifactFragment_55870beb-1c22-43a9-bdd9-45e0fc3cbfca", _Char, 1, 1);

// Not enough fragments to consume them - show message
IF CharacterUsedItemTemplate(_Char, "PIP_LOOT_UNI_ArtifactFragment_55870beb-1c22-43a9-bdd9-45e0fc3cbfca", _Item)
AND
ItemTemplateIsInPartyInventory(_Char, "PIP_LOOT_UNI_ArtifactFragment_55870beb-1c22-43a9-bdd9-45e0fc3cbfca", 0, _Count)
AND
_Count < 2
THEN
OpenMessageBox(_Char, "PIP_ArtifactFragment_NotEnough");
DB_PIP_CraftedArtifact(1);

// Consume fragments and grant a new Artifact
IF CharacterUsedItemTemplate(_Char, "PIP_LOOT_UNI_ArtifactFragment_55870beb-1c22-43a9-bdd9-45e0fc3cbfca", _Item)
AND
QRY_AMER_UI_Funds_CheckFunds_GetUserFunds(_Char, "PIP_LOOT_UNI_ArtifactFragment_55870beb-1c22-43a9-bdd9-45e0fc3cbfca")
AND
DB_AMER_GEN_OUTPUT_Integer(_Count)
AND
_Count > 1
THEN
PROC_PIP_ArtifactFragment_GrantNewArtifact(_Char);
ItemTemplateRemoveFromParty("PIP_LOOT_UNI_ArtifactFragment_55870beb-1c22-43a9-bdd9-45e0fc3cbfca", _Char, 2);
DB_PIP_CraftedArtifact(1); // a check for just one osiris frame, for the "cannot use from bags" warning.

// if items are in bags and not enough are in main inventory. Only show this if using the template failed (because was in bags)
IF CharacterUsedItemTemplate(_Char, "PIP_LOOT_UNI_ArtifactFragment_55870beb-1c22-43a9-bdd9-45e0fc3cbfca", _Item)
AND
NOT DB_PIP_CraftedArtifact(1)
AND
ItemTemplateIsInPartyInventory(_Char, "PIP_LOOT_UNI_ArtifactFragment_55870beb-1c22-43a9-bdd9-45e0fc3cbfca", 0, _Count)
AND
_Count > 1
THEN
OpenMessageBox(_Char, "PIP_CraftArtifact_InBags");

IF CharacterUsedItemTemplate(_Char, "PIP_LOOT_UNI_ArtifactFragment_55870beb-1c22-43a9-bdd9-45e0fc3cbfca", _Item)
AND
DB_PIP_CraftedArtifact(1)
THEN
NOT DB_PIP_CraftedArtifact(1);

// I don't think there is a way to generate treasure onto a character (at least from story), so we do a little workaround with a temporary container
PROC PROC_PIP_ArtifactFragment_GrantNewArtifact((CHARACTERGUID)_Char)
AND
GetPosition(_Char, _X, _Y, _Z)
AND
CreateItemTemplateAtPosition("LOOT_BackPack_A_6c70c298-aa29-418f-a659-f8e0b5f5fa60", _X, _Y, _Z, _Backpack)
AND
CharacterGetLevel(_Char, _Level)
THEN
GenerateTreasure(_Backpack, "ST_AMER_UNI", _Level, _Char);
MoveAllItemsTo(_Backpack, _Char, 0, 0, 1);
PlaySound(_Char, "GP_ScriptedEvent_HOE_Channel_Spell_Final");
PlayEffect(_Char, "AMER_RS3_FX_LootShine_Unique", "");
ItemDestroy(_Backpack);
SetOnStage(_Backpack, 0);

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start_AMER"
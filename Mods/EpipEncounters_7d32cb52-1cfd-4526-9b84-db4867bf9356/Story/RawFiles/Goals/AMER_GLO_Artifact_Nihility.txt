Version 1
SubGoalCombiner SGC_AND
INITSECTION

NOT DB_AMER_Artifacts_EquippedStatusMod_ExtendedStat("Artifact_Nihility", "Both", "AMER_WEAKENED_3", "InstantEffect", "ActivateVitalityVoid", "", "", 1.0, 2);
NOT DB_AMER_Artifacts_EquippedStatusMod_ExtendedStat("Artifact_Nihility", "Both", "AMER_DECAYING_3", "InstantEffect", "ActivateVitalityVoid", "", "", 1.0, 2);
NOT DB_AMER_Artifacts_EquippedStatusMod_ExtendedStat("Artifact_Nihility", "Both", "AMER_TERRIFIED_3", "InstantEffect", "ActivateVitalityVoid", "", "", 1.0, 2);

KBSECTION

// brand new script to allow Nihility to be restricted to once-per-round. Previous implementation used DB_AMER_Artifacts_EquippedStatusMod_ExtendedStat()

// When you apply Terrified III, Vulnerable III, or Weakened III to an enemy, activate Vitality Void. This effect can only occur once per round, per each compatible status applied.

PROC PROC_PIP_NihilityActivate((CHARACTERGUID)_Char, (STRING)_Status)
AND
QRY_AMER_GEN_OncePerRound(_Char, _Status)
THEN
PROC_AMER_KeywordStat_VitalityVoid_Fire(_Char, 1);
DB_AMER_GEN_OncePerRound(_Char, _Status);

PROC PROC_AMER_GEN_FilteredStatus_Applied((CHARACTERGUID)_Char, (CHARACTERGUID)_Source, "AMER_WEAKENED_3", (INTEGER)_Turns)
AND
QRY_PIP_CanProcNihility(_Source, _Char)
THEN
PROC_PIP_NihilityActivate(_Source, "Artifact_Nihility_Weakened");

PROC PROC_AMER_GEN_FilteredStatus_Applied((CHARACTERGUID)_Char, (CHARACTERGUID)_Source, "AMER_DECAYING_3", (INTEGER)_Turns)
AND
QRY_PIP_CanProcNihility(_Source, _Char)
THEN
PROC_PIP_NihilityActivate(_Source, "Artifact_Nihility_Decaying");

PROC PROC_AMER_GEN_FilteredStatus_Applied((CHARACTERGUID)_Char, (CHARACTERGUID)_Source, "AMER_TERRIFIED_3", (INTEGER)_Turns)
AND
QRY_PIP_CanProcNihility(_Source, _Char)
THEN
PROC_PIP_NihilityActivate(_Source, "Artifact_Nihility_Terrified");

// completes if char inflicting the status has nihility and victim is enemy.
QRY QRY_PIP_CanProcNihility((CHARACTERGUID)_StatusInflictor, (CHARACTERGUID)_Victim)
AND
DB_AMER_Artifacts_EquippedEffects(_ArtifactWielder, "Artifact_Nihility", _)
AND
_StatusInflictor == _ArtifactWielder
AND
CharacterIsEnemy(_Victim, _ArtifactWielder, 1)
THEN
DB_NOOP();

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_Artifacts"
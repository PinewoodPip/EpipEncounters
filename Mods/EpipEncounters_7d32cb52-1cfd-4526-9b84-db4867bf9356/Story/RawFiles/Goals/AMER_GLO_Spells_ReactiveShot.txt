Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_PIP_ReactionShotCompatibleReaction("AMER_Predator");
DB_PIP_ReactionShotCompatibleReaction("AMER_Centurion");

DB_PIP_ReactionShotFreeReactionStatuses("REACTION_SHOT_INFUS_2");
DB_PIP_ReactionShotFreeReactionStatuses("REACTION_SHOT_INFUS_3");

KBSECTION

// this script is NOT parented to ranger skills because the reaction AP override logic needs to always run. TODO move that single part out for performance

// SI1: +15% damage (+4% per Huntsman) to Predator/Centurion reactions for 2 turns.
QRY QRY_AMER_Source_Infusion_SkillPreCast((CHARACTERGUID)_Char, "Target_ReactionShot", (INTEGER)_InfusCount)
AND
_InfusCount > 0
THEN
ApplyStatus(_Char, "REACTION_SHOT_INFUS_1", 12.0, 1, _Char);

// SI2: free Predator/Centurion reactions for 2 turns.
QRY QRY_AMER_Source_Infusion_SkillPreCast((CHARACTERGUID)_Char, "Target_ReactionShot", (INTEGER)_InfusCount)
AND
_InfusCount > 1
THEN
ApplyStatus(_Char, "REACTION_SHOT_INFUS_2", 12.0, 1, _Char);

// SI3: Apply SI2 effect to all visible allied heroes within 13m. Recover 1SP.
QRY QRY_AMER_Source_Infusion_SkillPreCast((CHARACTERGUID)_Char, "Target_ReactionShot", (INTEGER)_InfusCount)
AND
_InfusCount > 2
THEN
PROC_AMER_GEN_IterateCharsAroundObject(_Char, 13.0, _Char, 12, "PIP_SourceInfusion_ReactionShot_3"); // condition 12: allies, not dead
PROC_AMER_Source_Infusion_AddRefund(_Char, 1, 1);

// Apply SI3 effect to allies in range.
PROC PROC_AMER_GEN_IterateCharsAroundObject_Iterated((INTEGER)_RequestID, "PIP_SourceInfusion_ReactionShot_3", (CHARACTERGUID)_SourceChar, (CHARACTERGUID)_Char)
THEN
ApplyStatus(_Char, "REACTION_SHOT_INFUS_3", 12.0, 1, _SourceChar);

// Override AP cost.
PROC PROC_AMER_Reaction_PayCost_SetAPCostOverride((CHARACTERGUID)_Char, (STRING)_ReactionID, (INTEGER)_APCost, (INTEGER)_AP)
AND
DB_PIP_ReactionShotCompatibleReaction(_ReactionID)
AND
QRY_PIP_HasFreeReactionsFromReactionShot(_Char)
THEN
DB_AMER_Reaction_APCostOverride(0);

QRY QRY_PIP_HasFreeReactionsFromReactionShot((CHARACTERGUID)_Char)
AND
DB_PIP_ReactionShotFreeReactionStatuses(_Status)
AND
HasActiveStatus(_Char, _Status, 1)
THEN
DB_NOOP();

// Apply damage boost on predator/centurion
PROC PROC_AMER_KeywordStat_Predator_PreFire((CHARACTERGUID)_Char, (CHARACTERGUID)_Target)
AND
HasActiveStatus(_Char, "REACTION_SHOT_INFUS_1", 1)
AND
CharacterGetAbility(_Char, "RangerLore", _HuntsmanPoints)
AND
Real(_HuntsmanPoints, _HuntsmanPointsReal)
AND
RealProduct(_HuntsmanPointsReal, 4.0, _ExtraDamage)
AND
RealSum(_ExtraDamage, 15.0, _Damage)
THEN
PROC_AMER_TempStat_FlexStat_Add(_Char, "Stat", "DAMAGEBOOST", _Damage, 2000, "Attack");

PROC PROC_AMER_KeywordStat_Centurion_PreFire((CHARACTERGUID)_Char, (CHARACTERGUID)_Target)
AND
HasActiveStatus(_Char, "REACTION_SHOT_INFUS_1", 1)
AND
CharacterGetAbility(_Char, "RangerLore", _HuntsmanPoints)
AND
Real(_HuntsmanPoints, _HuntsmanPointsReal)
AND
RealProduct(_HuntsmanPointsReal, 4.0, _ExtraDamage)
AND
RealSum(_ExtraDamage, 15.0, _Damage)
THEN
PROC_AMER_TempStat_FlexStat_Add(_Char, "Stat", "DAMAGEBOOST", _Damage, 2000, "Attack");

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start_AMER"
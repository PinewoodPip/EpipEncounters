Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

PROC PROC_PIP_Hotkey_UserRest((CHARACTERGUID)_Char)
AND
DB_PIP_Hotkey_UserRest(_Bool)
THEN
NOT DB_PIP_Hotkey_UserRest(_Bool);

PROC PROC_PIP_Hotkey_UserRest((CHARACTERGUID)_Char)
AND
DB_RestTemplates(_Template, _Consume, _Radius, _SafeRadius)
AND
ItemTemplateIsInPartyInventory(_Char, _Template, 0, _Count)
AND
_Count > 0
AND
QRY_AMER_GEN_Mutex_Clear()
AND
GetItemForItemTemplateInPartyInventory(_Char, _Template, _Item)
AND
QRY_AMER_GEN_Mutex()
THEN
// UserRest(_Char, _Consume, _Radius, _SafeRadius);
CharacterUseItem(_Char, _Item, "GEN_PIP_Hotkeys_Rest_Event");
DB_PIP_Hotkey_UserRest(1);

PROC PROC_PIP_Hotkey_UserRest((CHARACTERGUID)_Char)
AND
NOT DB_PIP_Hotkey_UserRest(1)
THEN
PROC_AMER_GEN_OpenQueuedMessageBox(_Char, "We do not have a bedroll to rest on!");

// REGION Instant toggle party link. Copied from the original script.
PROC PROC_PIP_Hotkey_TogglePartyLink((CHARACTERGUID)_Char)
AND
NOT QRY_PIP_TogglePartyLink_ShouldUnlink(_Char)  //If this fails, it means _Char is linked with no one.
AND
DB_IsPlayer(_Ally)  //Link all party members to _Char if above failed.
AND
_Ally != _Char
THEN
CharacterAttachToGroup(_Ally, _Char);

QRY QRY_PIP_TogglePartyLink_ShouldUnlink((CHARACTERGUID)_Char)
AND
DB_IsPlayer(_Ally)
AND
_Ally != _Char
AND
CharactersAreGrouped(_Char, _Ally, 1)
THEN
CharacterDetachFromGroup(_Ally);

// REGION Portable Respec Mirror.
PROC PROC_PIP_Hotkey_Respec((CHARACTERGUID)_Char)
AND
QRY_AMER_GEN_Mutex_Clear() // only interact with 1 item.
AND
GetItemForItemTemplateInPartyInventory(_Char, "UNI_PortableRespecMIrror_FTJ_e5459667-0fa0-4485-9091-b65b44486ac9", _Item)
AND
QRY_AMER_GEN_Mutex()
THEN
CharacterUseItem(_Char, _Item, "GEN_PortableRespecMirror_FTJ_Event");

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Start"